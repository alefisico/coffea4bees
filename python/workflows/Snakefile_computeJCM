import os
username = os.getenv("USER")

output_path = "output/JCM_2024_v2/"
config['dataset_location'] = "metadata/datasets_HH4b_2024_v2.yml"
config["analysis_container"] = "/cvmfs/unpacked.cern.ch/gitlab-registry.cern.ch/cms-cmu/coffea4bees:latest"

rule all:
    input:
        f"{output_path}histAll_NoJCM.coffea",
        f"{output_path}jetCombinatoricModel_SB_2024_v2.yml",

### Including modules
module analysis:
    snakefile: "rules/analysis"
    config: config

use rule analysis_all from analysis as analysis_noJCM with:
    output: f"{output_path}histAll_NoJCM.coffea"
    params:
        isam=config['dataset'],
        iy=config['year'],
        output="histAll_NoJCM.coffea",
        output_path=f"{output_path}",
        logname="histAll_NoJCM",
        metadata="analysis/metadata/HH4b_noJCM.yml",
        datasets=config['dataset_location'],
        user=username,
        run_performance=True
    log: f"{output_path}logs/analysis_all.log"


use rule make_JCM from analysis as make_new_JCM with:
    input: f"{output_path}histAll_NoJCM.coffea"
    output: f"{output_path}jetCombinatoricModel_SB_2024_v2.yml"
    params:
        output_dir = f"{output_path}"
    log: f"{output_path}logs/make_JCM.log"
